module src/lib/typechecker/typecheck

import map
import src/lib/desugar/coreAst
import src/lib/typechecker/unification
import src/lib/utils/errors
import src/lib/utils/effects
import src/lib/utils/helpers
import src/lib/utils/handlers
import src/lib/typechecker/types
import src/lib/lexer/position

/// effect for generating fresh type variables and equating types
interface Typing {
  def fresh(): TypingType
  def equate(t1: TypingType, t2: TypingType, pos: Position): Unit
}

/// effect for getting the expected return type in a function context
interface ReturnContext {
  def getExpectedType(): TypingType
}

// typechecks a program and returns a mapping from variable names to their inferred types
def typecheck(prog: Program): Map[String, TypeBinding] / { Exception[TypeError], Exception[NameError], Inspector[TypeBinding], Positioning } = {
  typecheckIncremental(prog, map::emptyGeneric[String, TypeBinding]())
}

/// typechecks a program and returns a mapping from variable names to their inferred types
/// accepts an initial environment of variable bindings to start from
def typecheckIncremental(prog: Program, initialEnv: Map[String, TypeBinding]): Map[String, TypeBinding] / { Exception[TypeError], Exception[NameError], Inspector[TypeBinding], Positioning } = {
  var equations: List[TypeEquation] = Nil()
  var counter = 0

  // extend the initial environment with built-in functions
  // prefers built-in types over initialEnv, because 'union' keeps the first mapping in case of duplicates
  val newInitialEnv = getBuiltinTypeEnv().mapUnion(initialEnv)

  with handleTypingContext(newInitialEnv)
  try {
    prog.foreach { stmt => checkStmt(stmt); () }
    val finalSubst = solve(equations)

    val currentEnv: Map[String, TypeBinding] = do backup()
    var updatedEnv = map::emptyGeneric[String, TypeBinding]()

    currentEnv.foreach { (name, binding) => {
        val concreteType = applySubstitution(binding.tpe, finalSubst)
        updatedEnv = updatedEnv.put(name, TypeBinding(concreteType, binding.isMutable))
      }
    }
    updatedEnv

  } with Typing {
    def fresh() = {
      val t = TVar(counter)
      counter = counter + 1
      resume(t)
    }

    def equate(t1: TypingType, t2: TypingType, pos) = {
      equations = Cons(TypeEquation(t1, t2, pos), equations)
      resume(())
    }
  } with LoopContext {
    def isInLoop() = resume(false)
  } with ReturnContext {
    def getExpectedType() = raise(TypeError(), "Return statement outside of function context")
  }
}

/// typecheck a single statement
def checkStmt(stmt: Stmt): TypingType / { Typing, Context[TypeBinding], Inspector[TypeBinding], Exception[TypeError], Positioning, LoopContext, ReturnContext } = stmt match {
  case ValDeclaration(name, typeAnno, value, pos) => {
    with at(pos) 
    val tExpr = checkExpr(value)
    do equate(tExpr, translateType(typeAnno), pos)
    do define(name, TypeBinding(tExpr, false))
    TUnit()
  }

  case VarDeclaration(name, typeAnno, value, pos) => {
    with at(pos) 
    val tExpr = checkExpr(value)
    do equate(tExpr, translateType(typeAnno), pos)
    do define(name, TypeBinding(tExpr, true))
    TUnit()
  }

  case AssignStmt(name, value, pos) => {
    with at(pos) 
    val binding = do lookup(name)
    if (not(binding.isMutable)) {
      raise[Unit, TypeError](TypeError(), "Cannot assign to immutable variable '" ++ name ++ "' at " ++ do getPos().show)
    }
    do equate(binding.tpe, checkExpr(value), pos)
    TUnit()
  }

  case IfStmt(cond, thenBranch, elseBranch, pos) => {
    with at(pos) 
    // check the condition but don't enforce its type to be boolean yet. The truthiness analysis is done in the interpreter.
    checkExpr(cond)
    handleNewTypingScope { checkBlock(thenBranch) }
    handleNewTypingScope { checkBlock(elseBranch) }
  }

  case InspectScope() => {
    val currentContext = do backup()
    do inspect(currentContext)
    TUnit()
  }

  case ExprStmt(expr, pos) => {
    with at(pos) 
    checkExpr(expr)
    TUnit()
  }

  case WhileStmt(condition, body, pos) => {
    with at(pos) 
    // check the condition but don't enforce its type to be boolean yet. The truthiness analysis is done in the interpreter.
    checkExpr(condition)

    with handleNewTypingScope
    try {
      checkBlock(body)
    } with LoopContext {
      def isInLoop() = resume(true)
    }
    TUnit()
  }

  case BreakStmt(pos) => {
    if (not(do isInLoop())) {
      raise(TypeError(), "'break' is only allowed inside a loop " ++ pos.show)
    }
    TUnit()
  }

  case ContinueStmt(pos) => {
    if (not(do isInLoop())) {
      raise(TypeError(), "'continue' is only allowed inside a loop " ++ pos.show)
    }
    TUnit()
  }

  case ReturnStmt(maybeExpr, pos) => {
    with at(pos)
    val expectedType = do getExpectedType()

    val actualType = maybeExpr match {
      case Some(expr) => checkExpr(expr)
      case None() => TUnit()
    }

    do equate(actualType, expectedType, pos)
    TUnit()
  }
}

def checkBlock(block: Block): TypingType / { Typing, Context[TypeBinding], Inspector[TypeBinding], Exception[TypeError], Positioning, LoopContext, ReturnContext} = {
  block.statements.foreach { stmt => checkStmt(stmt); () }
  block.finalExpr match {
    case Some(expr) => checkExpr(expr)
    case None() => TUnit()
  }
}

/// Typecheck an expression and return its inferred type
def checkExpr(expr: Expr): TypingType / { Typing, Context[TypeBinding], Positioning, Exception[TypeError], Inspector[TypeBinding], LoopContext } = expr match {
  case IntLiteral(value, pos) => { with at(pos); TInt()}
  case BoolLiteral(value, pos) => { with at(pos); TBool()}
  case StringLiteral(value, pos) => { with at(pos); TString()}
  case DoubleLiteral(value, pos) => { with at(pos); TDouble()}
  case Variable(name, pos) => {
    with at(pos) 
    val binding = do lookup(name)
    binding.tpe
  }

  case AddExpr(left, right, pos) => {
    with at(pos) 
    val tLeft = checkExpr(left)
    val tRight = checkExpr(right)

    (tLeft, tRight) match {
      case (TString(), _) => {
        do equate(tLeft, TString(), pos)
        do equate(tRight, TString(), pos)
        TString()
      }
      case (_, TString()) => {
        do equate(tRight, TString(), pos)
        do equate(tLeft, TString(), pos)
        TString()
      }
      case _ => {
        checkNumeric(left, right, pos)
      }
    }
  }

  case SubExpr(left, right, pos) => {
    with at(pos) 
    checkNumeric(left, right, pos)
  }

  case MultExpr(left, right, pos) => {
    with at(pos) 
    checkNumeric(left, right, pos)
  }

  case DivExpr(left, right, pos) => {
    with at(pos) 
    checkNumeric(left, right, pos)
  }

  case IfExpr(condition, thenExpr, elseExpr, pos) => {
    with at(pos) 
    // check the condition but don't enforce its type to be boolean yet. The truthiness analysis is done in the interpreter.
    val tCond = checkExpr(condition)
    val tThen = checkExpr(thenExpr)
    val tElse = checkExpr(elseExpr)
    
    do equate(tThen, tElse, pos)
    tThen
  }

  case AndExpr(left, right, pos) => {
    with at(pos) 
    // check both sides but don't enforce their types to be boolean yet. The truthiness analysis is done in the interpreter.
    val tLeft = checkExpr(left)
    val tRight = checkExpr(right)
    TBool()
  }

  case OrExpr(left, right, pos) => {
    with at(pos) 
    // check both sides but don't enforce their types to be boolean yet. The truthiness analysis is done in the interpreter.
    val tLeft = checkExpr(left)
    val tRight = checkExpr(right)
    TBool()
  }

  case NotExpr(inner, pos) => {
    with at(pos) 
    // check the condition but don't enforce its type to be boolean yet. The truthiness analysis is done in the interpreter.
    checkExpr(inner)
    TBool()
  }

  case Eq(left, right, pos) => {
    with at(pos) 
    val tLeft = checkExpr(left)
    val tRight = checkExpr(right)
    do equate(tLeft, tRight, pos)
    TBool()
  }

  case Neq(left, right, pos) => {
    with at(pos) 
    val tLeft = checkExpr(left)
    val tRight = checkExpr(right)
    do equate(tLeft, tRight, pos)
    TBool()
  }

  case Lt(left, right, pos) => {
    with at(pos) 
    checkNumeric(left, right, pos)
    TBool()
  }

  case Gt(left, right, pos) => {
    with at(pos) 
    checkNumeric(left, right, pos)
    TBool()
  }

  case LtE(left, right, pos) => {
    with at(pos) 
    checkNumeric(left, right, pos)
    TBool()
  }

  case GtE(left, right, pos) => {
    with at(pos) 
    checkNumeric(left, right, pos)
    TBool()
  }

  case FunExpr(params, retAnno, body, pos) => {
    with at(pos) 
    val paramTypes = params.map { p => 
      val pType = translateType(p.tpe)

      // validate default value if present
      p.defaultValue match {
        case Some(defaultExpr) => {
          val tDefault = checkExpr(defaultExpr)
          // make sure default value matches parameter type
          do equate(tDefault, pType, pos)
        }
        case None() => ()
      } 
      TypingParam(pType, p.defaultValue.isDefined())
    }

    val retType = translateType(retAnno)

    handleNewTypingScope {
      params.zip(paramTypes).foreach { case (p, tp) => 
        do define(p.name, TypeBinding(tp.tpe, false))
      }
      
      val tBody = try {
        checkBlock(body)
      } with ReturnContext {
        def getExpectedType() = resume(retType)
      }
      
      do equate(tBody, retType, pos)
    }
    TFun(paramTypes, retType)
  }

  case CallExpr(receiver, args, pos) => {
    with at(pos) 

    receiver match {
      // special case for 'print' to allow any argument types and arbitrary number of arguments
      case Variable("print", _) => {
        args.foreach { arg => checkExpr(arg.value); () }
        TUnit()
      } 

      case Variable(name, _) and name == "max" || name == "min" => {
        if (args.size < 2) {
          raise(TypeError(), "'" ++ name ++ "' requires at least two arguments at " ++ pos.show)
        }

        val memberType = instantiate(TGen(0)) 

        args.foreach { arg => {
          val tArg = checkExpr(arg.value)
          assertIsNumeric(tArg, pos)
          do equate(tArg, memberType, pos)
        } }
        memberType
      }

      case _ => {
        val tReceiverRaw = checkExpr(receiver)
        val tReceiver = instantiate(tReceiverRaw) // TGen -> TVar
        val retType = do fresh()

        def isVariadicCall(params: List[TypingParam]): Bool = {
          params.reverse match {
            case Cons(p, _) => p.tpe match {
              case TVariadic(_) => true
              case _ => false
            }
            case _ => false
          }
        }
        
        tReceiver match {
          
          // variadic function call
          case TFun(params, retTypeDefined) and isVariadicCall(params) => {
            
            // helper function to check arguments against parameters
            def checkArgs(pList: List[TypingParam], aList: List[Argument]): Unit = {
              pList match {
                case Nil() => if (aList.size > 0) raise(TypeError(), "Too many arguments")
                
                // variadic parameter
                case Cons(param, restParams) => param.tpe match {
                  case TVariadic(innerType) => {
                    aList.foreach { arg => do equate(checkExpr(arg.value), innerType, pos) }
                  }

                  case _ => aList match {
                    // no more arguments check if parameter is optional
                    case Nil() => {
                      if (not(param.isOptional)) raise(TypeError(), "Missing argument")
                      checkArgs(restParams, Nil())
                    }

                    // match head argument to parameter and recurse
                    case Cons(arg, restArgs) => {
                      do equate(checkExpr(arg.value), param.tpe, pos)
                      checkArgs(restParams, restArgs)
                    }
                  }
                }
              }
            }

            checkArgs(params, args)
            do equate(retType, retTypeDefined, pos)
          }

          // non-variadic function call
          case _ => {
            val argTypes = args.map { arg => checkExpr(arg.value) }
            val tCall = TFun(argTypes.map { t => TypingParam(t, false) }, retType)
            
            // unify the receiver type with the call type
            do equate(tReceiver, tCall, pos)
          }
        }
        
        retType
      }
    }
  }
}

/// check that both sides are numeric types and return the resulting type
def checkNumeric(left: Expr, right: Expr, pos: Position): TypingType / { Typing, Context[TypeBinding], Positioning, Exception[TypeError], Inspector[TypeBinding], LoopContext } = {
  val tLeft = checkExpr(left)
  val tRight = checkExpr(right)

  assertIsNumeric(tLeft, pos)
  assertIsNumeric(tRight, pos)

  do equate(tLeft, tRight, pos)
  tLeft
}

/// assert that a type is numeric (int or double)
def assertIsNumeric(t: TypingType, pos: Position): Unit / { Typing, Positioning, Exception[TypeError] } = t match {
  case TInt() => ()
  case TDouble() => ()
  case TVar(_) => () // defer check until unification
  case _ => raise(TypeError(), "Expected numeric type, but got " ++ t.show ++ " at " ++ pos.show)
}

/// translate an optional Type annotation into a TypingType
def translateType(t: Option[Type]): TypingType / Typing = t match {
  case Some(IntType()) => TInt()
  case Some(BoolType()) => TBool()
  case Some(UnitType()) => TUnit()

  case Some(FunType(paramTypes, returnType)) => {
    val tParams = paramTypes.map { pt => TypingParam(translateType(Some(pt)), false) }
    val tReturn = translateType(Some(returnType))
    TFun(tParams, tReturn)
  }

  case Some(StringType()) => TString()
  case Some(DoubleType()) => TDouble()

  case None() => do fresh() // generate a fresh type variable
}

/// instantiate a type by replacing all negative type variables with fresh type variables
def instantiate(t: TypingType): TypingType / Typing = {
  // mapping from generic type variable ids to fresh type variables
  var mapping = map::emptyGeneric[Int, TypingType]()

  def go(tp: TypingType): TypingType / Typing = tp match {
    // replace generic type variables with fresh type variables
    case TGen(id) => {
      mapping.get(id) match {
        case Some(freshT) => freshT
        case None() => {
          val freshT = do fresh()
          mapping = mapping.put(id, freshT)
          freshT
        }
      }
    }

    // recursively instantiate inner type
    case TVariadic(inner) => TVariadic(go(inner))

    // recursively instantiate function parameter and return types
    case TFun(paramTypes, retType) => {
      TFun(paramTypes.map { p => TypingParam(go(p.tpe), p.isOptional) }, go(retType))
    }
    case _ => tp
  }
  go(t)
}

/// constructs the built-in type environment
def getBuiltinTypeEnv(): Map[String, TypeBinding] = {
  map::fromListGeneric[String, TypeBinding]([
    // here TVar(-1) represents a generic type parameter which gets instantiated upon use
    ("print", TypeBinding(TFun([TypingParam(TVariadic(TGen(0)), false)], TUnit()), false)),
    ("input", TypeBinding(TFun([TypingParam(TString(), true)], TString()), false)),
    ("str", TypeBinding(TFun([TypingParam(TGen(0), false)], TString()), false)),
    ("max", TypeBinding(TFun([
        TypingParam(TGen(0), false), // first Arg
        TypingParam(TGen(0), false), // second Arg
        TypingParam(TVariadic(TGen(0)), false) // variadic Args
      ], TGen(0)), false)),
    ("min", TypeBinding(TFun([
        TypingParam(TGen(0), false), // first Arg
        TypingParam(TGen(0), false), // second Arg
        TypingParam(TVariadic(TGen(0)), false) // variadic Args
      ], TGen(0)), false))
  ])
}
